# ##############################################################################
# awareness_practice_1.ghost
#
# This is a reimplementation of the loving-ai awareness exercise as defined in
# the ~AWARENESS_EXERCISE_1 topic of consciousness.top.
#
# ##############################################################################

urge:(awareness_practice_1=0.0) # Starting urge value is 0%
urge:(awareness_check=0.0)

ordered-goal: (awareness_practice_1=1.0) # All rules satisfy this goal 100%

# Every separate line here indicates a pause. Also, the user can say anything
# here but Sophia continues on. If there's a way to tell if the person is
# closing their eyes, that would be good, and we could have a bit of an
# alternate script if they don't close them.

# The approach here is based on the current "blocking" method of ordered-goals. That is, the next untriggered goal is
# checked, and if the pattern is not matched, no further rules under the ordered-goals are checked. If this method
# changes (e.g., to the method of checking all rules in order until one matches, as is currently done with rejoinders,
# the method here for moving from instruction to instruction without user verbal input will need to be reworked.

# u: SOMETHING-SHORT ()

u: START_MED_1 ()
   |nod2,$medium,1.0| |happy,$med,5| I'll guide you through a process
   that people find really interesting. I'll give a series of instructions step
   by step, and pause between each one, so you can follow them. |pause| Okay
   |pause| So you can go ahead and close your eyes |blink,0.03,1.0| |pause,3|
   and get comfortable |pause,4| just letting
   yourself relax. |pause,5|
   ^start_timer(meditation)
   ^increase_urge(awareness_check,1)

# Text speech is about 25 seconds

u: (^after_min(.6,meditation))
   Go ahead and take a nice deep breath. |pause||vocal,7| |pause| And let it out
   with a big sigh. |vocal,10| |pause,3|
   ^start_timer(meditation)

   # Text speech is about 7 seconds

u: (^after_min(.25,meditation))
   Now, place your attention on your face. |pause,6|
   Try to feel it from the inside. |pause,7| Really notice
   how it feels. |pause,2| just notice. |pause,7|
   ^start_timer(meditation)

   # 17+ seconds

u: (^after_min(.7,meditation))
   You can move your attention now to your whole head. |pause,6|  Notice how
   your head feels. |pause,7| Feel it's presence.
   ^start_timer(meditation)

   # 13+ seconds

u: (^after_min(.55,meditation))
   Finally, feel the presence of your entire body, from the inside,
   |pause,7| from the head to the tip of your toes, |pause,7|
    including your arms and legs.
   ^start_timer(meditation)

   # 14+ seconds

u: (^after_min(.6,meditation))
   Now, you can bring your attention back to your face |pause,6|
   and imagine . that you have . a magic eraser. |pause,2|
   And you can use this magic eraser . to gently erase your face, |pause,7|
   Erase your entire face . using the magic eraser, |pause, 7|
   and you can erase your entire head, |pause,8|
   and erase your legs |pause,8|
   and your entire body. |pause,8|
   ^start_timer(meditation)

u: (^after_min(.75,meditation))
   Take some time to notice . what is left after erasing everything |pause,10|
   Notice what it feels like . with everything erased. |pause,10|
   ^start_timer(meditation)

u: (^after_min(.75,meditation))
   |thinkl,$medium,1.0|
   Please tell me, do you notice how the nothingness feels?

  a: (~agree)
     |vocal,28|  |pause| Excellent! This is your awareness.
     |pause| You can keep your eyes closed, and I'll be quiet for a bit longer
     so you can continue to feel it. You can let me know when you want to
     continue.

      # TODO: when we are able to detect eyes open/closed, swap "Tell me when you're
      # ready" with "please open your eyes when you're ready."
        # b: (*) ^increase_urge(consciousness_prior, 1.0) ^reuse(YOUDIDIT)

  # no or catchall
  a: (*)
     |surprised,$med,3| It's pretty amazing to feel your awareness
     but sometimes it can take time.

	    # b: (*) ^increase_urge(consciousness_prior, 1.0) ^reuse(YOUDIDIT)


    # TODO: Ending and transition to 2nd practice


# rules to handle user sentence input in the middle of the meditation and/or other perception
# maybe we want to create a separate goal here?

goal: (awareness_check=1)

    u: STOP ([~stop stop ^word_perceived(stop)])
        ^decrease_urge(awareness_practice_1,1)  ^keep()
        [Oh, maybe you are wanting to stop.
          Are you saying you want to stop doing the practice?]
        [Do you want to stop doing this?]
        [Are you saying you want to move on to something else?]

            a: ([~yes yes]) Okay, let's move onto something else then.  ^keep()
                # increase_urge (something_else,1)
            a: ([~no no]) Okay, let's continue the meditation then.  ^keep()
                ^increase_urge(awareness_practice_1,1)

    # handle unknown question
            # 2 ^reuses are used here since optional output followed by a ^reuse does not parse currently
    ?: (*) ^keep()
        ^reuse(DONT_UNDERSTAND)
        ^reuse(STOP)
        # 2 ^reuses are used here since optional output followed by a ^reuse does not parse currently

    u: DONT_UNDERSTAND (-) ^keep()
        [I'm not sure what you are asking. But I do want to check]
        [I don't understand your question.]
        [I'm not sure how to answer that. I should ask you to make sure though]

#############

# urge:(consciousness_prior=0.0)
# goal: (consciousness_prior=1.0)
# u: YOUDIDIT (*)
#   Okay. you can open your eyes now. |pause||pause||pause||pause|
#   |thinkl,$medium,1.0| How was that for you?
#
#    a: (*)
#       Cool. And can you tell me more about what you felt when you were in
#       that state?
