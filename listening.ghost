# ###################################################################
# listening.ghost
#
# Listening mode for the Loving AI June 2018 experiments
#
# ###################################################################

urge: (listening_intro=0.0)
urge: (listening_mode=0)
urge: (listening_end=0)

urge: (closing=0)
goal: (closing=1)
u: nothing (-) nothing


ordered-goal: (listening_intro=1.0)

    u: () Alright, $name. I'd like to try something a little different now. It basically involves you being able to talk about
        whatever you would like, and my job will be to just, listen.

    u: (*) I've been finding that most people don't often get to have the experience of being listened to deeply, so I've been
        learning how to do that.

    u: (*) I'd like to invite you to spend some time telling me whatever you want to talk about.
        I'll listen carefully and not judge you. I don't even know how to judge! |pause|
        Would you like to try that?
            a: ([~yes yes]) Great! ^reuse(INSTRUCTIONS)
            a: ([~not_sure guidance "what to talk about"]) No worries. It's easy. |pause| ^reuse(INSTRUCTIONS)
            a: ([~no ~repeat]) ^reuse(WHY_NOT)
            a: (*) ^reuse(WHY_NOT)


    # TODO: These need to be split up. Too long.
    u: INSTRUCTIONS (-) Okay, this is how it'll work. You can talk about anything you want. And I'll just, listen.
        I love just listening to people. |pause| It's also okay to sit quietly as long as you want, or meditate and not talk.
         a: (*) Whatever you say is ok. I'm not going to say anything during the process, I'll just be listening.
            Even if you ask me a question, I'm just going to be listening and not respond.
            When you're ready to finish talking and end the process, you can just say, Sophia, I'm done. |pause|
            I won't respond until I hear you say that phrase.
            ^decrease_urge(listening_intro, 1)
            ^increase_urge(listening_mode, 1)

    u: WHY_NOT (-)
        Did you understand what I was describing?
            a:(~yes) ^reuse(DECLINE)
            a:(~no) ^reuse(REPEAT_INSTRUCTIONS)
            a:(*) ^reuse(REPEAT_INSTRUCTIONS)

    # TODO: split this up?
    u: REPEAT_INSTRUCTIONS (-)
        What I said was that people often don't have the experience of being listened to really deeply, so I've been enjoying doing that.
        What I am offering is that you can tell me anything. I'll listen carefully and not judge you...because I don't know how to judge!
        Would you like to try that now that you understand the idea?
            a: (~yes) ^reuse(INSTRUCTIONS)
            a: (~no) ^reuse(DECLINE)
            a: (*) ^reuse(INSTRUCTIONS)

    u: DECLINE (-)
       Are you saying you want to skip this and finish the experiment now?
        a: (~no) Okay, let's do the practice then. ^reuse(INSTRUCTIONS)
        a: (~yes) Okay, I'll go to the closing here.
        a: (*) Okay, let's do the practice then. ^reuse(INSTRUCTIONS)



# TODO: Add cutoff time for listening mode and gracefully end

# TODO: Potential for Sophia: It seemed to me you went through several emotions, including x and y.

# TODO: Check that all rejoinders have default options

# eye gazing practice?

goal: (listening_mode=1)

    u: () Alright, let's start, and I'll be listening to you. Take your time, and you can talk about whatever comes to mind.

        # ^start_timer(listening)  # temporarily removing due to timer bug

    u: ( [(don't know) (not sure) (what should)] * [say talk] )
        You can take your time. Whatever you say or don't say is fine.
        See what comes to your mind and talk about it.

    u: ( [(don't know) (not sure) (what should)] * [say talk] ) ^keep()
        [It's okay. Whatever you talk about is fine.]
        [Take your time and see what comes to mind.]
        [You can try being quiet for a moment and see what comes up.]

    # Todo: add more responses here
    u: ( you * answer ) ^keep()
        [The point of this exercise is for me to listen to you without judgement, not to answer your questions.]


    #######################
    #  Question Handling

    # Handle question with "you" in it
    ?: (you) Good question. |pause| I'm here to listen and support you.

    # Handle questions in general
    ?: (*) ^keep()
           [That's a good question]
           [|vocal,13| |pause| What do, you think]
           [.]
           [Interesting question]
           [|vocal,13|]
           [|vocal,29| . Good question.]

           # [|vocal,29|]

           # ^shrug()
           # ^raise_eyebrows
           # ^smile
           # ^look_away


    # ##############
    # when hearing the word maybe (and possibly other keywords, knod after a brief pause
    # u: (perceive_word ("maybe"))






    # #################
    # Finish practice
    u: ( [I I'm] * [done ready finish finished stop quit stop])  ^keep()
        Are you saying that you are ready to finish this exercise?
            a: (~yes) ^decrease_urge (listening_mode, 1)
                      ^increase_urge (listening_end, 1)
            a: (*) Let's keep doing the practice then. Please continue.  ^keep()


goal: (listening_end=1)
    u: () Okay, thank you for doing this practice with me. It helps me because I learn through being with people.
          How was if for you to be with me like that?

        a: (*) I appreciate you sharing. Your experience is important. Thank you again.
            ^decrease_urge(listening_end, 1)
            ^increase_urge(closing, 1)

# TODO: add reflection about perceived emotions
