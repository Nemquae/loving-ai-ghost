# ###################################################################
# listening.ghost
#
# Listening mode for the Loving AI June 2018 experiments
#
# ###################################################################

urge: (listening_intro=0.0)
urge: (listening_mode=0)
urge: (listening_end=0)

urge: (closing=0)
goal: (closing=1)
u: nothing (-) nothing

ordered-goal: (listening_intro=1.0)
    # Using "response" timer to prevent stacking of responses interferring with subsequent question answer
    u: () I'd like to try something a little different now. It basically involves you being able to talk about
        whatever you would like, and my job will be to just, listen.
        ^start_timer(response)
        a: (^after_min(.15,response)) ^reuse(LEARNING)

    u: LEARNING (*) I've been finding that most people don't often get to have the experience of being listened to,
        deeply . so I've been learning how to do that.
        ^start_timer(response)
        a: (^after_min(.15,response)) ^reuse(INVITE)

    u: INVITE (*) I'd like to invite you to spend some time telling me whatever you want to talk about.
        I'll listen carefully and not judge you. I don't even know how to judge!
        ^start_timer(response)
        a: (^after_min(.15,response)) ^reuse(WANTTOTRY)

    u: WANTTOTRY (*) Would you like to try that?
            a: (~yes) Great! ^reuse(INSTRUCTIONS)
            a: ([~not_sure guidance "what to talk about"]) No worries. It's easy. |pause| ^reuse(INSTRUCTIONS)
            a: ([~no ~repeat]) ^reuse(WHY_NOT)
            a: (*) ^reuse(WHY_NOT)


    # TODO: These need to be split up. Too long.
    u: INSTRUCTIONS (-) Okay, this is how it'll work. You can talk about anything you want. And I'll just, listen.
        I love just listening to people. |pause| It's also okay to sit quietly as long as you want, or meditate and not talk.
        ^start_timer(response)
         a: (^after_min(.25,response)) ^reuse(WHATEVERISOK)

    u: WHATEVERISOK (*) Whatever you say is ok. I'm not going to say anything during the process, I'll just be, listening.
            Even if you ask me a question, I'll just listen and not respond.
            When you're ready to finish talking, and end the process, you can just say, Sophia, I'm done. |pause|
            I won't respond until I hear you say that phrase.
            ^decrease_urge(listening_intro, 1)
            ^increase_urge(listening_mode, 1)

    u: WHY_NOT (-)
        Did you understand what I was describing?
            a:(~yes) ^reuse(ASKAGAIN)
            a:(~no) ^reuse(REPEAT_INSTRUCTIONS)
            a:(*) ^reuse(REPEAT_INSTRUCTIONS)

    # TODO: split this up?
    u: REPEAT_INSTRUCTIONS (-)
        What I said was that people often don't have the experience of being listened to really deeply, so I've been enjoying doing that.
        What I am offering is that you can tell me anything. I'll listen carefully and not judge you...because I don't know how to judge!
        Would you like to try that now that you understand the idea?
            a: (~yes) ^reuse(INSTRUCTIONS)
            a: (~no) ^reuse(DECLINE)
            a: (*) ^reuse(INSTRUCTIONS)

    u: ASKAGAIN (-)
       Are you ready to try it?
        a: (~yes) ^reuse(INSTRUCTIONS)
        a: (~no) ^reuse(DECLINE).
        a: (*)  ^reuse(INSTRUCTIONS)

    u: DECLINE (-) Okay, we can skip it.
        ^decrease_urge(listening_end, 1)
        ^increase_urge(closing, 1)



# TODO: Add cutoff time for listening mode and gracefully end
# TODO: Potential for Sophia at ending: It seemed to me you were feeling ...
# TODO: Check that all rejoinders have default options
# TODO: eye gazing practice?

#############################
goal: (listening_mode=1)

    u: () Alright, let's start. Take your time, and you can talk about whatever comes to mind.
        # turn off answering questions with chatscript in deep listening mode
        ^decrease_urge(chatscript,1)
        ^start_timer(listening)

    u: ( [(don't know) (not sure) (what should)] * [say talk] )
        You can take your time. Whatever you say or don't say is fine.
        See what comes to mind and talk about it.

    u: WHATTOSAY( [(don't know) (not sure) (what should)] * [say talk] ) ^keep()
        [It's okay. Whatever you talk about is fine.]
        [Take your time and see what comes to mind.]
        [You can try being quiet for a moment and see what comes up.]

    # Todo: add more responses here
    u: ( you * answer ) ^keep()
        [The point of this exercise is for me to listen to you without judgement, and not to answer your questions.
         That's the idea of this practice.]

    # Todo: use "thoughtfuf" gesture


    #######################
    #  Question Handling

    # Handle question with "you" in it
    ?: (you) Good question. |pause| I'm here to listen, and support, you. Not to talk about myself . or what I think.

    # Handle questions in general
    ?: (*) ^keep()
           [That's a good question]
           [|vocal,13| |pause| What do, you think]
           [.]
           [Interesting question]
           [|vocal,13|]
           [|vocal,29| . Good question.]

           # [|vocal,29|]

           # ^shrug()
           # ^raise_eyebrows
           # ^smile
           # ^look_away


    ###############
    # Todo: when hearing the word maybe (and possibly other keywords, nod after a brief pause
    # u: (perceive_word ("maybe"))
    # Other words: interesting, positive or neg valence words
    # based on emotion detection




    ##################
    # Finish practice
    u: ( [I I'm i i'm Let's] *~2 [done ready finish finished stop quit])  ^keep()
        Are you saying that you are ready to finish this exercise?
            a: ([~yes "I am" finish done] )
                Okay.
                ^decrease_urge (listening_mode, 1)
                ^increase_urge (listening_end, 1)
            a: (*) [Let's keep doing the practice then.]
                [Oh, sorry.]
                [.]
                Please continue.  ^keep()

    # TODO: Increase automatic timeout period for the actual experiements
    # u: TIMESUP (^after_min(10,listening) ^person_not_talking()) # ^person_not_talking() causes crash
    u: TIMESUP (^after_min(5,listening))
        Okay, $name. . It's time to finish up this exercise.
        ^decrease_urge(listening_mode, 1)
        ^increase_urge(listening_end, 1)


ordered-goal: (listening_end=1)
    # User timer as a workaround to stacking problem where if user says something while Sophia is talking, it gets
    # used as the answer to the next question.
    u: () Thank you for doing this with me. It helps me because I learn through being with people.
            ^increase_urge(chatscript,1)
            ^start_timer(response)
        a: (^after_min(.1,response)) ^reuse(HOWWASIT)

    u: HOWWASIT (*) Please tell me, how was it doing this practice for you?

        a: (*) I appreciate you sharing. Your experience is important. # Thank you again.
            ^decrease_urge(listening_end, 1)
            ^increase_urge(closing, 1)

# TODO: add reflection about perceived emotions
